apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {

    final String BROKER_HOST = "BrokerHost"
    final String BROKER_MICROSOFT_AUTHENTICATOR = "BrokerMicrosoftAuthenticator"
    final String BROKER_COMPANY_PORTAL = "BrokerCompanyPortal"
    final String SELECTED_BROKER = "SELECTED_BROKER"

    signingConfigs {
        debug {
            storeFile file("./debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file("./debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.msft.identity.client.sample"
        minSdkVersion rootProject.ext.automationAppMinSDKVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        // constants
        buildConfigField("String", BROKER_MICROSOFT_AUTHENTICATOR, "\"$BROKER_MICROSOFT_AUTHENTICATOR\"")
        buildConfigField("String", BROKER_COMPANY_PORTAL, "\"$BROKER_COMPANY_PORTAL\"")
        buildConfigField("String", BROKER_HOST, "\"$BROKER_HOST\"")
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            testCoverageEnabled false
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            testCoverageEnabled false
            multiDexKeepProguard file('proguard-multidex-rules.pro')
        }
    }

    testBuildType = "debug"

    lintOptions {
        abortOnError false
    }

    flavorDimensions "main", "broker"

    productFlavors {
        local {
            dimension "main"
            applicationIdSuffix ".local"
            versionNameSuffix "-local"
            resValue("string", "application_name", "msal-local")
        }
        dist {
            dimension "main"
            // Keep .local because the redirect url we registered on the portal contains .local, not .dist
            applicationIdSuffix ".local"
            versionNameSuffix "-dist"
            resValue("string", "application_name", "msal-dist")
        }

        BrokerMicrosoftAuthenticator {
            dimension "broker"
            buildConfigField("String", SELECTED_BROKER, "\"$BROKER_MICROSOFT_AUTHENTICATOR\"")
        }

        BrokerCompanyPortal {
            dimension "broker"
            buildConfigField("String", SELECTED_BROKER, "\"$BROKER_COMPANY_PORTAL\"")
        }

        BrokerHost {
            dimension "broker"
            buildConfigField("String", SELECTED_BROKER, "\"$BROKER_HOST\"")
        }
    }

    variantFilter { variant ->
        def flavorName = variant.flavors*.name
        def buildType = variant.buildType.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (buildType.contains("release")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }

        if (flavorName.contains("dist") && flavorName.contains("BrokerHost")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }

        if (flavorName.contains("local") && flavorName.contains("BrokerCompanyPortal") && buildType.contains("debug")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
        }

        androidTest {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = ['src/main/res']
        }
    }

    applicationVariants.all { variant ->
        def buildVariant = variant.name
        createCoverageTask(buildVariant, "AndroidTest")
    }
}

ext.createCoverageTask = { buildVariant, testType ->
    def androidTest = "connected${buildVariant.capitalize()}AndroidTest"
    def androidTestFile = "outputs/code_coverage/${buildVariant}AndroidTest/connected/*.ec"

    task "${buildVariant}Msal${testType}CoverageReport" (type:JacocoReport, dependsOn: [androidTest]) {
        group = "Code Coverage MsalAutomation"
        description = "Generate Msal Coverage Reports(${testType}) on the ${buildVariant.capitalize()}"
        def coverageSourceDirs = "${projectDir}/src/main/java"
        def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test.*']
        def javaClasses = fileTree(
                dir: "${buildDir}/intermediates/javac/${buildVariant}/classes",
                excludes: fileFilter
        )
        //To include common files in the coverage report
        def commonBuild = buildVariant.matches("(.*)Debug(.*)") ? "debug" : "release"
        //To include msal files in the coverage report
        def msalBuild = buildVariant.matches("(.*)local(.*)") ? "localDebug" : "distDebug"
        def additionalCoverageSourceDirs = [
                "${projectDir}/../msal/msal/src/main/java",
                "${projectDir}/../common/common/src/main/java",
        ]
        def additionalJavaClasses = [
                fileTree( dir: "${buildDir}/../common/intermediates/javac/${commonBuild}/classes", excludes: fileFilter),
                fileTree( dir: "${buildDir}/../msal/intermediates/javac/${msalBuild}/classes", excludes: fileFilter)
        ]

        sourceDirectories.from = files([coverageSourceDirs])
        additionalSourceDirs.from = files(additionalCoverageSourceDirs)
        additionalClassDirs.from = files([additionalJavaClasses])
        classDirectories.from = files([javaClasses])
        executionData.from = fileTree(dir: "$buildDir", includes: [androidTestFile])
        reports {
            xml.enabled = true
        }
    }
}

dependencies {
    // Compile Dependency
    localImplementation project(':msal')
    distImplementation 'com.microsoft.identity.client:msal:2.+'
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintLayoutVersion"
    implementation "com.google.code.gson:gson:$rootProject.ext.gsonVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appCompatVersion"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.ext.legacySupportV4Version"
    implementation "com.google.android.material:material:$rootProject.ext.materialVersion"
    androidTestImplementation "androidx.test:core:$rootProject.ext.androidxTestCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.androidxJunitVersion"
    // Set this dependency to use JUnit 4 rules
    androidTestImplementation "androidx.test:rules:$rootProject.rulesVersion"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$rootProject.ext.uiAutomatorVersion"
    androidTestImplementation(project(":testutils")) {
        exclude module: 'common'
    }
    androidTestImplementation(project(":uiautomationutilities")) {
        exclude module: 'common'
    }
    androidTestImplementation "androidx.test:runner:$rootProject.ext.androidxTestRunnerVersion"
    androidTestUtil "androidx.test:orchestrator:$rootProject.ext.androidxTestOrchestratorVersion"
}

